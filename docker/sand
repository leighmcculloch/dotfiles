#!/usr/bin/env zsh
set -euo pipefail

SCRIPT_DIR="${0:a:h}"
DOCKERFILE="$SCRIPT_DIR/../../docker/sand.Dockerfile"

# Select tool
TOOL="${TOOL:-$(gum choose --header "Select AI tool:" "opencode" "claude" "copilot")}"

# Build image
docker build \
  -t "$TOOL" \
  --target "$TOOL" \
  --build-arg HOME="$HOME" \
  --build-arg USER="$USER" \
  --build-arg UID="$(id -u)" \
  -f "$DOCKERFILE" \
  "$SCRIPT_DIR"

# Build volume mounts
MOUNTS=()
MOUNT_DIRS=(
  "$(pwd)"
  "$HOME/.claude.json"
  "$HOME/.claude"
  "$HOME/.copilot"
  "$HOME/.config/opencode"
  "$HOME/.local/share/opencode"
  "$HOME/.scripts"
)
# Mount common git dir if current dir or any immediate subdir is a worktree
typeset -A seen_common_dirs
for dir in "$(pwd)" "$(pwd)"/*/; do
  [[ -d "$dir" ]] || continue
  git -C "$dir" rev-parse --is-inside-work-tree &>/dev/null || continue
  git_dir="$(git -C "$dir" rev-parse --git-dir)"
  git_common_dir="$(git -C "$dir" rev-parse --git-common-dir)"
  [[ "$git_dir" != "$git_common_dir" ]] || continue
  common_dir="$(cd "$dir" && cd "$(dirname "$git_common_dir")" && pwd)"
  [[ -z "${seen_common_dirs[$common_dir]:-}" ]] || continue
  seen_common_dirs[$common_dir]=1
  MOUNT_DIRS+=("$common_dir")
done
for dir in "${MOUNT_DIRS[@]}"; do
    MOUNTS+=(-v "${dir}:${dir}")
done

# Mount API key files if they exist (warn if missing)
KEYFILES=(
  "$HOME/.zenv_apikey_perplexity"
  "$HOME/.zenv_apikey_context7"
  "$HOME/.zenv_apikey_pushover"
  "$HOME/.zenv_apikey_github_ro"
)
for keyfile in "${KEYFILES[@]}"; do
  MOUNTS+=(-v "${keyfile}:${keyfile}:ro")
done

# Get git identity from host
GIT_USER_NAME="$(git config --global user.name 2>/dev/null || true)"
GIT_USER_EMAIL="$(git config --global user.email 2>/dev/null || true)"

# Generate container name from current directory + random 2-digit suffix
CONTAINER_NAME="$(basename "$(pwd)" | tr -d '.')-$(printf '%02d' $((RANDOM % 100)))"

# Load any keys needed
source "$HOME/.zenv_apikey_github_ro"

# Run container
docker run --rm -it \
  --privileged=false \
  --cap-drop=ALL \
  --security-opt=no-new-privileges:true \
  --name "$CONTAINER_NAME" \
  "${MOUNTS[@]}" \
  -e "GIT_AUTHOR_NAME=$GIT_USER_NAME" \
  -e "GIT_AUTHOR_EMAIL=$GIT_USER_EMAIL" \
  -e "GIT_COMMITTER_NAME=$GIT_USER_NAME" \
  -e "GIT_COMMITTER_EMAIL=$GIT_USER_EMAIL" \
  -e "GH_TOKEN=$GITHUB_TOKEN" \
  -w "$(pwd)" \
  "$TOOL" \
  "$@"
