#!/usr/bin/env zsh
set -euo pipefail

SCRIPT_DIR="${0:a:h}"
DOCKERFILE="$SCRIPT_DIR/../../docker/sand.Dockerfile"

# Select tool
TOOL="${TOOL:-$(gum choose --header "Select AI tool:" "opencode" "claude")}"

# Build image
docker build \
  -t "$TOOL" \
  --target "$TOOL" \
  --build-arg HOME="$HOME" \
  --build-arg USER="$USER" \
  --build-arg UID="$(id -u)" \
  -f "$DOCKERFILE" \
  "$SCRIPT_DIR"

# Build volume mounts
MOUNTS=()
MOUNT_DIRS=(
  "$(pwd)"
  "$HOME/.claude.json"
  "$HOME/.claude"
  "$HOME/.config/opencode"
  "$HOME/.local/share/opencode"
  "$HOME/.scripts"
)
if git rev-parse --is-inside-work-tree &>/dev/null; then
  # Mount common git dir repo if this is a worktree
  local git_dir git_common_dir
  git_dir="$(git rev-parse --git-dir)"
  git_common_dir="$(git rev-parse --git-common-dir)"
  if [[ "$git_dir" != "$git_common_dir" ]]; then
    common_dir="$(dirname "$git_common_dir")"
    MOUNT_DIRS+=("$common_dir")
  fi
fi
for dir in "${MOUNT_DIRS[@]}"; do
    MOUNTS+=(-v "${dir}:${dir}")
done

# Mount API key files if they exist (warn if missing)
KEYFILES=(
  "$HOME/.zenv_apikey_perplexity"
  "$HOME/.zenv_apikey_context7"
  "$HOME/.zenv_apikey_pushover"
  "$HOME/.zenv_apikey_github_ro"
)
for keyfile in "${KEYFILES[@]}"; do
  MOUNTS+=(-v "${keyfile}:${keyfile}:ro")
done

# Get git identity from host
GIT_USER_NAME="$(git config --global user.name 2>/dev/null || true)"
GIT_USER_EMAIL="$(git config --global user.email 2>/dev/null || true)"

# Run container
docker run --rm -it \
    "${MOUNTS[@]}" \
    -e "GIT_AUTHOR_NAME=$GIT_USER_NAME" \
    -e "GIT_AUTHOR_EMAIL=$GIT_USER_EMAIL" \
    -e "GIT_COMMITTER_NAME=$GIT_USER_NAME" \
    -e "GIT_COMMITTER_EMAIL=$GIT_USER_EMAIL" \
    -w "$(pwd)" \
    "$TOOL" \
    "$@"
