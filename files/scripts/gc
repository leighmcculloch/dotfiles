#!/usr/bin/env zsh

set -o nounset

function {
  local message=""
  local yes=0
  while [[ $# -gt 0 ]]; do
    case "$1" in
      -m)
        if [[ -n "$2" ]]; then
          message="$2"
          shift 2
          break
        fi
        ;;
      -y)
        yes=1
        ;;
    esac
    shift
  done

  if [[ -n "$message" ]]; then
    git commit -m "$message" "$@"
    return
  fi

  if git rev-parse -q --verify MERGE_HEAD >/dev/null 2>&1; then
    git commit "$@"
    return
  fi

  local branch="${${$(git branch --format='%(refname:short)' --show-current | jq -R -s '.')%\"}#\"}"

  local diff="$(git diff --staged)"
  if [ -z "$diff" ]; then
    echo "No staged changes"
    return 1
  fi

  local prompt="You are a senior software engineer.

Given the following git diff, suggest a concise, commit message that says in present tense with imperative mood what has changed without saying why (max 50 characters).

Reply only with the commit message, nothing else. Do not use convention commit format. Start the commit message with a lower case letter and do not end with a full stop. Avoid generic phrases.

Branch: ${branch}

Diff:
${diff}"

  # AI configurations: full command (prompt will be appended as last arg)
  local -a ai_configs=(
    #"claude --model claude-opus-4-5 -p"
    #"claude --model claude-sonnet-4-5 -p"
    #"claude --model claude-haiku-4-5 -p"
    #"opencode run -m github-copilot/claude-opus-4.5"
    "copilot --model claude-opus-4.5 -p"
    #"copilot --model claude-sonnet-4.5 -p"
    #"copilot --model claude-haiku-4.5 -p"
    #"copilot --model gpt-5.2-codex -p"
    #"copilot --model gpt-5.1-codex-max -p"
    #"copilot --model gpt-5.2 -p"
    #"copilot --model gpt-5.1-codex-mini -p"
    #"copilot --model gpt-5-mini -p"
    #"copilot --model gemini-3-pro-preview -p"
  )

  # If -y flag, pick one random model and use it directly
  if (( yes == 1 )); then
    local random_idx=$(( RANDOM % ${#ai_configs[@]} + 1 ))
    local cmd="${ai_configs[$random_idx]}"

    printf "○ %s\n" "$cmd"
    local start_time=$(gdate +%s%6N)
    message="$(${=cmd} "$prompt" 2>/dev/null | tr '\n' ' ' | sed 's/  */ /g' | sed 's/^ //;s/ $//')"
    local end_time=$(gdate +%s%6N)
    local elapsed_us=$((end_time - start_time))
    local elapsed_s=$(printf '%d.%06d' $((elapsed_us / 1000000)) $((elapsed_us % 1000000)))
    printf "\033[1A● %s (${elapsed_s}s)\033[K\n" "$cmd"

    if [[ -z "$message" ]]; then
      echo "Failed to generate commit message" >&2
      return 1
    fi
  else
    local -a pids=()
    local -a tmpfiles=()
    local -a errfiles=()
    local -a labels=()
    local -a start_times=()
    local -a elapsed_times=()
    local total=${#ai_configs[@]}

    # Launch all AI calls in parallel
    for cmd in "${ai_configs[@]}"; do
      labels+=("$cmd")

      local tmpfile="$(mktemp)"
      local errfile="$(mktemp)"
      tmpfiles+=("$tmpfile")
      errfiles+=("$errfile")

      start_times+=($(gdate +%s%6N))
      ${=cmd} "$prompt" > "$tmpfile" 2>"$errfile" &
      pids+=($!)
    done

    # Progress display - print initial state
    for i in {1..${#pids[@]}}; do
      printf "○ %s\n" "${labels[$i]}"
    done

    # Initialize elapsed times array
    for i in {1..${#pids[@]}}; do
      elapsed_times+=(-1)
    done

    local completed=0
    while (( completed < total )); do
      completed=0
      printf "\033[${total}A"  # Move up
      for i in {1..${#pids[@]}}; do
        if kill -0 ${pids[$i]} 2>/dev/null; then
          printf "○ %s\033[K\n" "${labels[$i]}"
        else
          if (( elapsed_times[$i] < 0 )); then
            elapsed_times[$i]=$(($(gdate +%s%6N) - start_times[$i]))
          fi
          local elapsed_s=$(printf '%d.%06d' $((elapsed_times[$i] / 1000000)) $((elapsed_times[$i] % 1000000)))
          printf "● %s (${elapsed_s}s)\033[K\n" "${labels[$i]}"
          ((completed++))
        fi
      done
      (( completed < total )) && sleep 0.5
    done

    # Clear progress display
    printf "\033[${total}A"
    for i in {1..${total}}; do
      printf "\033[K\n"
    done
    printf "\033[${total}A"

    # Collect results
    local -a choices=()
    for i in {1..${#tmpfiles[@]}}; do
      local result="$(cat "${tmpfiles[$i]}" | tr '\n' ' ' | sed 's/  */ /g' | sed 's/^ //;s/ $//')"
      if [[ -n "$result" ]]; then
        local elapsed_s=$(printf '%d.%06d' $((elapsed_times[$i] / 1000000)) $((elapsed_times[$i] % 1000000)))
        choices+=("${result} [${labels[$i]} (${elapsed_s}s)]")
      else
        echo "FAILED: ${labels[$i]}" >&2
        echo "  stdout: $(cat "${tmpfiles[$i]}")" >&2
        echo "  stderr: $(cat "${errfiles[$i]}")" >&2
      fi
    done

    # Clean up
    for tmpfile in "${tmpfiles[@]}"; do
      rm -f "$tmpfile"
    done
    for errfile in "${errfiles[@]}"; do
      rm -f "$errfile"
    done

    choices+=("[type my own]")

    local choice="$(printf '%s\n' "${choices[@]}" | gum choose --header "Select commit message:")"

    if [[ -z "$choice" ]]; then
      echo "Cancelled"
      return 1
    fi

    if [[ "$choice" == "[type my own]" ]]; then
      message=""
    else
      message="${choice% \[*\]}"
      # Print selected model so it stays in history
      local selected_label="${choice##*\[}"
      selected_label="${selected_label%\]}"
      printf "● %s\n" "$selected_label"
    fi

    message="$(gum write --width 80 --char-limit 99999 --prompt '| ' --value "$message")"
  fi

  git commit -m "$message" ${@:+"$@"}
} ${@:+"$@"}
